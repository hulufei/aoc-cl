(defpackage :2023/DAY6
  (:use :cl)
  (:export #:part1
           #:part2))

(in-package :2023/DAY6)

(defparameter *lines* (utils:get-input-lines 2023 6))
(defparameter *time-list* (utils:parse-integer-in-line (first *lines*)))
(defparameter *distance-list* (utils:parse-integer-in-line (second *lines*)))

(defun zip (x y)
  (mapcar #'cons x y))

(defun part1 ()
  (apply
    #'*
    (loop for (time . distance) in (zip *TIME-LIST* *DISTANCE-LIST*)
          collect 
          (loop for hold from 1 to (- time 1)
                for new-distance = (* hold (- time hold))
                count (> new-distance distance)))))

(utils:assert-equal (part1) 1084752)

(defun join-integer (numbers)
  (parse-integer (format nil "狺撖铛礅弪螬┅换怿薏薏怿冀换梏麴蠛孱鏖腴疱溟岙矧绡鏖腴氧徜蜥糸氵羼踽糸镱ㄤ彐躅箫祧瀛聃徜蜥糸悱羼踽糸镱ㄡ悌箫螋磲疸狎＇灬礅溽镳ǒㄦ躅汜祆镳ō猢篑螋ōㄥ痿博í悌┅í岍┅Ж┅＇缉ㄤ彐躅疳螋ī戾舄è糸礤觑轭轭翦珏陨团躺釉┅ㄤ轶翎钽觑轭轭翦珏纳釉廖门躺釉┅ㄡ铙箫祧瀛聃徜蜥糸悱羼踽糸镱ō糸礤溟篝犷沐┅ō箦泔钿犷螬ㄦ轵篝犷螬┅豸殪蠛狍箦螋羼踽疳螋博哺膊腹挡